/*
 * SPDX-License-Identifier: MIT
 *
 * Copyright (c) 2015-2018 Andres Almiray
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 */

plugins {
    id 'org.kordamp.gradle.kordamp-parentpom' version '1.2.1'
    id 'org.kordamp.gradle.source-xref'       version '0.12.0' apply false
}

allprojects {
    apply plugin: 'idea'

    dependencyUpdates.resolutionStrategy = {
        componentSelection { rules ->
            rules.all { selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                }
                if (rejected) {
                    selection.reject('Release candidate')
                }
            }
        }
    }

    license {
        mapping {
            fxml = 'XML_STYLE'
        }
        exclude '**/*.png'
        exclude '**/*.jpg'
        exclude '**/*.scss'
    }
}

config {
    info {
        description   = 'BootstrapFX: CSS and widgets'
        inceptionYear = '2015'
        tags          = ['javafx','css','bootstrap']

        specification { enabled = false }
    }

    license {
        licenses {
            license {
                id      = 'MIT'
                primary = true
            }
        }
    }

    javadoc {
        options {
            links('http://docs.oracle.com/javase/8/docs/api/',
                  'http://docs.oracle.com/javase/8/javafx/api/')
        }
    }
}

subprojects { subproj ->
    apply plugin: 'java'

    subproj.tasks.withType(JavaCompile) {
        sourceCompatibility = subproj.sourceCompatibility
        targetCompatibility = subproj.targetCompatibility
    }
}
