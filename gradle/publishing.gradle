/*
 * Copyright (c) 2015-2018 Andres Almiray
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 */

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

task generateMinPom {
    doLast {
        String pomHeader = """|<?xml version="1.0" encoding="UTF-8"?>
            |<project xmlns="http://maven.apache.org/POM/4.0.0"
            |         xsi:schemaLocation="http://maven.apache.org/POM/4.0.0 http://maven.apache.org/xsd/maven-4.0.0.xsd"
            |         xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance">
            |    <modelVersion>4.0.0</modelVersion>
            |    <groupId>${project.group}</groupId>
            |    <artifactId>${project.name}</artifactId>
            |    <version>${project.version}</version>
            |""".stripMargin('|')

        def dependencyTemplate = { dep -> """|        <dependency>
                                             |            <groupId>$dep.group</groupId>
                                             |            <artifactId>$dep.name</artifactId>
                                             |            <version>$dep.version</version>
                                             |        </dependency>""".stripMargin('|')
        }

        String deps = configurations.runtime.allDependencies.findAll({it.name!= 'unspecified'})
            .collect({ dep -> dependencyTemplate(dep)}).join('\n')

        String pom = pomHeader
        if (deps) {
            pom += "    <dependencies>\n$deps\n    </dependencies>\n"
        }
        pom += "</project>"

        project.file("$buildDir/tmp/maven").mkdirs()
        project.file("$buildDir/tmp/maven/pom.xml").text = pom
        project.file("$buildDir/tmp/maven/pom.properties").text = """|# Generated by Gradle ${gradle.gradleVersion}
            |version=${project.version}
            |groupId=${project.group}
            |artifactId=${project.name}
            |""".stripMargin('|')
    }
}

jar {
    dependsOn 'generateMinPom'
    manifest {
        attributes(
            'Created-By': buildCreatedBy,
            'x-Built-By': buildBy,
            'x-Build-Jdk': buildJdk,
            'x-Build-Date': buildDate,
            'x-Build-Time': buildTime,
            'x-Build-Revision': buildRevision,
            'Specification-Title': project.name,
            'Specification-Version': project.version,
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Automatic-Module-Name': project.moduleName
        )
    }
    metaInf {
        from(rootProject.files('.')) {
            include 'LICENSE*'
        }
        from(file("$buildDir/tmp/maven")) {
            into "maven/${project.group}/${project.name}"
        }
    }
}

publishing {
    publications {
        mavenCustom(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = project.name
                description = project.project_description
                url = project.project_url
                packaging = 'jar'
                inceptionYear = '2015'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://opensource.org/licenses/MIT'
                        distribution = 'repo'
                    }
                }
                scm {
                    url = project.project_scm
                }
                developers {
                    [
                        aalmiray : 'Andres Almiray'
                    ].each { devId, devName ->
                        developer {
                            id = devId
                            name = devName
                            roles = ['Developer']
                        }
                    }
                }
            }
        }
    }
}

if (!project.hasProperty('bintrayUsername')) ext.bintrayUsername = ''
if (!project.hasProperty('bintrayApiKey')) ext.bintrayApiKey = ''
if (!project.hasProperty('mavenUsername')) ext.mavenUsername = ''
if (!project.hasProperty('mavenPassword')) ext.mavenPassword = ''

bintray {
    user = project.bintrayUsername
    key = project.bintrayApiKey
    publications = ['mavenCustom']
    pkg {
        repo = project.project_bintray_repo
        userOrg = project.project_bintray_org
        name = project.name
        desc = project.project_description
        licenses = ['MIT']
        labels = ['javafx','css','bootstrap']
        websiteUrl = project.project_url
        issueTrackerUrl = project.project_issues
        vcsUrl = project.project_scm
        publicDownloadNumbers = true
        githubRepo = 'aalmiray/bootstrapfx'
        version {
            name = project.version
            vcsTag = "${project.name}-${project.version}"
            mavenCentralSync {
                sync = true
                user = project.mavenUsername
                password = project.mavenPassword
            }
        }
    }
}
